#東方弾幕風[Single]
#ScriptVersion[3]
#Title["マイクロスレッド (GATP無し)"]
#Text[""]
#Image[""]
#System["/script/default_system/Default_System.txt"]
#Background["script/default_system/Default_Background_IceMountain.txt"]
#BGM[DEFAULT]
#Player[DEFAULT]

#include "script/default_system/Default_ShotConst.txt"

let objEnemy;

@Event {
  alternative(GetEventType)
  case(EV_REQUEST_LIFE) {
    SetScriptResult(2000);
  }
}

@Initialize {
  objEnemy = ObjEnemy_Create(OBJ_ENEMY_BOSS);
  ObjEnemy_Regist(objEnemy);

  let imgExRumia = GetCurrentScriptDirectory ~ "img/ExRumia.png";
  ObjPrim_SetTexture(objEnemy, imgExRumia);
  ObjSprite2D_SetSourceRect(objEnemy, 64, 1, 127, 64);
  ObjSprite2D_SetDestCenter(objEnemy);

  let cx = GetStgFrameWidth / 2;
  ObjMove_SetDestAtFrame(objEnemy, cx, 60, 58);

  study009;
  study012;
  endProcess;
}

@MainLoop {
  ObjEnemy_SetIntersectionCircleToShot(objEnemy, ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 32);
  ObjEnemy_SetIntersectionCircleToPlayer(objEnemy, ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 24);
  yield;
}

task study009 {
  loop(60) {yield;}

  loop {
    let speed = 1;
    loop(6) {
      CreateShotA1(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), speed, GetAngleToPlayer(objEnemy), DS_BALL_S_RED, 0);
      speed += 0.5;
      loop(10) {yield;}
    }
    loop(60) {yield;}
  }
}

task study012 {
  loop(60) {yield;}

  loop {
    let angle = rand(0, 360);
    loop(16) {
      CreateShotA1(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 2, angle, DS_BALL_S_RED, 0);
      angle += 360/16;
    }
    loop(60) {yield;}
  }
}

task endProcess {
  while(ObjEnemy_GetInfo(objEnemy, INFO_LIFE) > 0) {yield;}
  Obj_Delete(objEnemy);
  CloseScript(GetOwnScriptID);
  return;
}

#include "./function.txt"
