#東方弾幕風[Single]
#ScriptVersion[3]
#Title["オブジェクト弾 (オブジェクト弾から自機狙い弾) (GATP, rand_int無し)"]
#Text[""]
#Image[""]
#System["/script/default_system/Default_System.txt"]
#Background["script/default_system/Default_Background_IceMountain.txt"]
#BGM[DEFAULT]
#Player[DEFAULT]

#include "script/default_system/Default_ShotConst.txt"

let objEnemy;

@Event {
  alternative(GetEventType)
  case(EV_REQUEST_LIFE) {
    SetScriptResult(2000);
  }
}

@Initialize {
  objEnemy = ObjEnemy_Create(OBJ_ENEMY_BOSS);
  ObjEnemy_Regist(objEnemy);

  let imgExRumia = GetCurrentScriptDirectory ~ "img/ExRumia.png";
  ObjPrim_SetTexture(objEnemy, imgExRumia);
  ObjSprite2D_SetSourceRect(objEnemy, 64, 1, 127, 64);
  ObjSprite2D_SetDestCenter(objEnemy);

  let cx = GetStgFrameWidth / 2;
  ObjMove_SetDestAtFrame(objEnemy, cx, 60, 58);

  mainTask;
  endProcess;
}

@MainLoop {
  ObjEnemy_SetIntersectionCircleToShot(objEnemy, ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 32);
  ObjEnemy_SetIntersectionCircleToPlayer(objEnemy, ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 24);
  yield;
}

task mainTask {
  loop(60) {yield;}

  loop {
    let sign = rand_int0(1)*2 - 1;
    loop(4) {
      objectBullet1(GetAngleToPlayer(objEnemy) + 60*sign);
      sign *= -1;
      loop(60) {yield;}
    }
    loop(120) {yield;}

    objectBullet2(GetAngleToPlayer(objEnemy)-20);
    objectBullet2(GetAngleToPlayer(objEnemy)+20);
    loop(600) {yield;}
  }
}

task objectBullet1 (let angle) {
  let obj = CreateShotA1(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 4, angle, DS_BALL_BS_RED, 0);

  let count = 0;
  while(!Obj_IsDeleted(obj)) {
    if(count%3 == 0) {
      CreateShotA1(ObjMove_GetX(obj), ObjMove_GetY(obj), 2+count*0.1, GetAngleToPlayer(objEnemy), DS_NEEDLE_RED, 0);
    } else if(count >= 30) {
      Obj_Delete(obj);
    }
    count++;
    yield;
  }
}

task objectBullet2 (let angle) {
  let obj = CreateShotA1(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 12, angle, DS_BALL_BS_RED, 0);

  while(!Obj_IsDeleted(obj)) {
    CreateShotA1(ObjMove_GetX(obj), ObjMove_GetY(obj), 1, GetAngleToPlayer(objEnemy), DS_NEEDLE_RED, 0);
    yield;
  }
}

task endProcess {
  while(ObjEnemy_GetInfo(objEnemy, INFO_LIFE) > 0) {yield;}
  Obj_Delete(objEnemy);
  CloseScript(GetOwnScriptID);
  return;
}

#include "./function.txt"
