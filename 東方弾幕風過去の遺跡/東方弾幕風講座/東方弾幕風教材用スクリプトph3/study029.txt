#“Œ•û’e–‹•—[Single]
#ScriptVersion[3]
#Title["ƒIƒuƒWƒFƒNƒg’e (‰æ–Ê˜g’…’eŽ®ày—ô’e)"]
#Text[""]
#Image[""]
#System["/script/default_system/Default_System.txt"]
#Background["script/default_system/Default_Background_IceMountain.txt"]
#BGM[DEFAULT]
#Player[DEFAULT]

#include "script/default_system/Default_ShotConst.txt"

let objEnemy;

@Event {
  alternative(GetEventType)
  case(EV_REQUEST_LIFE) {
    SetScriptResult(2000);
  }
}

@Initialize {
  objEnemy = ObjEnemy_Create(OBJ_ENEMY_BOSS);
  ObjEnemy_Regist(objEnemy);

  let imgExRumia = GetCurrentScriptDirectory ~ "img/ExRumia.png";
  ObjPrim_SetTexture(objEnemy, imgExRumia);
  ObjSprite2D_SetSourceRect(objEnemy, 64, 1, 127, 64);
  ObjSprite2D_SetDestCenter(objEnemy);

  let cx = GetStgFrameWidth / 2;
  ObjMove_SetDestAtFrame(objEnemy, cx, 60, 58);

  mainTask;
  endProcess;
}

@MainLoop {
  ObjEnemy_SetIntersectionCircleToShot(objEnemy, ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 32);
  ObjEnemy_SetIntersectionCircleToPlayer(objEnemy, ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 24);
  yield;
}

task mainTask {
  loop(60) {yield;}

  loop {
    let angle = rand(0, 360);
    loop(32) {
      objectBullet1(angle);
      angle += 360/32;
    }
    loop(180) {yield;}

    angle = 70;
    loop(5) {
      objectBullet2(angle);
      angle += 10;
      loop(15) {yield;}
    }
    loop(720) {yield;}
  }
}

task objectBullet1 (let angle) {
  let obj = CreateShotA1(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 4, angle, DS_BALL_BS_BLUE, 0);

  while(!Obj_IsDeleted(obj)) {
    if(ObjMove_GetX(obj) < 0) {
      let speed = 0.4;
      loop(12) {
	CreateShotA1(ObjMove_GetX(obj), ObjMove_GetY(obj), speed, 0, DS_RICE_S_BLUE, 0);
	speed += 0.1;
      }
      Obj_Delete(obj);
    } else if(ObjMove_GetX(obj) > GetStgFrameWidth) {
      let speed = 0.4;
      loop(12) {
	CreateShotA1(ObjMove_GetX(obj), ObjMove_GetY(obj), speed, 180, DS_RICE_S_BLUE, 0);
	speed += 0.1;
      }
      Obj_Delete(obj);
    }
    yield;
  }
}

task objectBullet2 (let angle) {
  let obj = CreateShotA1(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 3, angle, DS_BALL_L_RED, 0);
  ObjShot_SetSpellResist(obj, true);

  while(!Obj_IsDeleted(obj)) {
    if(ObjMove_GetY(obj) > GetStgFrameHeight) {
      let angle = rand(0, 360);
      loop(32) {
	CreateShotA1(ObjMove_GetX(obj), ObjMove_GetY(obj), 0.7, angle, DS_BALL_S_RED, 0);
	angle += 360/32;
      }
      Obj_Delete(obj);
    }
    yield;
  }
}

task endProcess {
  while(ObjEnemy_GetInfo(objEnemy, INFO_LIFE) > 0) {yield;}
  Obj_Delete(objEnemy);
  CloseScript(GetOwnScriptID);
  return;
}

#include "./function.txt"
