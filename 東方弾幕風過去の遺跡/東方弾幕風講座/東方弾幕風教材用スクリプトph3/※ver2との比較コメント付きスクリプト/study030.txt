#東方弾幕風[Single]	//プログラム中のコメントアウトについて
#ScriptVersion[3]	//★　ver2には無かった処理をする関数
#Title["オブジェクト弾 (画面枠で反射する弾) (rand_int無し)"]	//≒　ver2に似たような関数がある
#Text[""]		//無印　ver2にほぼ同じ関数がある (ただし処理の適用範囲が拡張されているものが多いため、注釈付き)
#Image[""]		//未記入　verに同名、同じ処理の関数がある
#System["/script/default_system/Default_System.txt"]
#Background["script/default_system/Default_Background_IceMountain.txt"]
#BGM[DEFAULT]
#Player[DEFAULT]

#include "script/default_system/Default_ShotConst.txt"

let objEnemy;

@Event {
  alternative(GetEventType)	//★イベントの種類を取得する関数
  case(EV_REQUEST_LIFE) {	//★引数の文字列はライフ設定要求
    SetScriptResult(2000);	//≒ SetLife (EV_REQUEST_LIFEと併用)
  }
}

@Initialize {
  objEnemy = ObjEnemy_Create(OBJ_ENEMY_BOSS);	//Obj_Create のボス版
  ObjEnemy_Regist(objEnemy);			//★オブジェクトのアクティブ化

//☆ 注意 ☆  スクリプトパスのディレクトリの区切りは『\』ではなく『/』で行う
  let imgExRumia = GetCurrentScriptDirectory ~ "img/ExRumia.png";
  ObjPrim_SetTexture(objEnemy, imgExRumia);		//ObjEffect_SetTexture ボスもオブジェクトなので、ボスIDに関連付け
  ObjSprite2D_SetSourceRect(objEnemy, 64, 1, 127, 64);	//SetGraphicRect のオブジェクト版
  ObjSprite2D_SetDestCenter(objEnemy);			//★指定したオブジェクトの原点をSetSourceRectで抜き出した画像の中心にセットする

  let cx = GetStgFrameWidth / 2;			//≒ GetCenterX に相当する計算
  ObjMove_SetDestAtFrame(objEnemy, cx, 60, 58);		//SetMovePosition02 相当だが、オブジェクトにも有効

  mainTask;
  endProcess;
}

@MainLoop {
  ObjEnemy_SetIntersectionCircleToShot(objEnemy, ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 32);	//SetCollisionA GetX GetY
  ObjEnemy_SetIntersectionCircleToPlayer(objEnemy, ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 24);	//SetCollisionB GetX GetY
  yield;
}

task mainTask {
  loop(60) {yield;}

  loop {
    loop(4) {
      let angle = rand(0, 360);
      loop(24) {
	objectBullet(angle);
	angle += 360/24;
      }
      loop(90) {yield;}
    }
    loop(180) {yield;}

	//SetMovePosition02(GetCenterX, GetCenterY - 40, 60);
    ObjMove_SetDestAtFrame(objEnemy, GetStgFrameWidth/2, GetStgFrameHeight/2 - 40, 60);
    loop(60) {yield;}

    let sign = rand_int0(1)*2 - 1;	//rand_int(0, 1) 相当
    loop(4) {
      let angle = rand(0, 360);
      loop(6) {
	loop(16) {
	  objectBullet(angle);
	  angle += 360/16;
	}
	angle += 0.7*sign;
	loop(5) {yield;}
      }
      sign *= -1;
      loop(60) {yield;}
    }
    loop(180) {yield;}

	//SetMovePosition02(GetCenterX, GetClipMinY + 60, 60);
    ObjMove_SetDestAtFrame(objEnemy, GetStgFrameWidth/2, 60, 60);
    loop(60) {yield;}
  }
}

task objectBullet (let angle) {
	//面倒なのでph3ならではの記述で
  let obj = CreateShotA1(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 2, angle, DS_NEEDLE_BLUE, 0);

  while(!Obj_IsDeleted(obj)) {		//Obj_BeDeleted(obj)
	//if((Obj_GetX(obj) < GetClipMinX) || (Obj_GetX(obj) > GetClipMaxX)) {
    if((ObjMove_GetX(obj) < 0) || (ObjMove_GetX(obj) > GetStgFrameWidth)) {
	//CreateShot01(Obj_GetX(obj), Obj_GetY(obj), 2, 180-Obj_GetAngle(obj), AQUA11, 0);
	//ph3ではAQUAではなくSKY
      CreateShotA1(ObjMove_GetX(obj), ObjMove_GetY(obj), 2, 180-ObjMove_GetAngle(obj), DS_NEEDLE_SKY, 0);
      Obj_Delete(obj);
	//} else if(Obj_GetY(obj) < GetClipMinY) {
    } else if(ObjMove_GetY(obj) < 0) {
	//CreateShot01(Obj_GetX(obj), Obj_GetY(obj), 2, 360-Obj_GetAngle(obj), AQUA11, 0);
      CreateShotA1(ObjMove_GetX(obj), ObjMove_GetY(obj), 2, 360-ObjMove_GetAngle(obj), DS_NEEDLE_SKY, 0);
      Obj_Delete(obj);
    }
    yield;
  }
}

task endProcess {
  while(ObjEnemy_GetInfo(objEnemy, INFO_LIFE) > 0) {yield;}	//≒ GetLife 相当
  Obj_Delete(objEnemy);
  CloseScript(GetOwnScriptID);		//★スクリプトの終了を行う　★自分のスクリプトIDを取得
  return;
}

#include "./function.txt"
