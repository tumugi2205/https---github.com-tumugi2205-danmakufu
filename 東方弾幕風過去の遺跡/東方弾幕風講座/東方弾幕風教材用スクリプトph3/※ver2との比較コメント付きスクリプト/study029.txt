#東方弾幕風[Single]	//プログラム中のコメントアウトについて
#ScriptVersion[3]	//★　ver2には無かった処理をする関数
#Title["オブジェクト弾 (画面枠着弾式炸裂弾)"]	//≒　ver2に似たような関数がある
#Text[""]		//無印　ver2にほぼ同じ関数がある (ただし処理の適用範囲が拡張されているものが多いため、注釈付き)
#Image[""]		//未記入　verに同名、同じ処理の関数がある
#System["/script/default_system/Default_System.txt"]
#Background["script/default_system/Default_Background_IceMountain.txt"]
#BGM[DEFAULT]
#Player[DEFAULT]

#include "script/default_system/Default_ShotConst.txt"

let objEnemy;

@Event {
  alternative(GetEventType)	//★イベントの種類を取得する関数
  case(EV_REQUEST_LIFE) {	//★引数の文字列はライフ設定要求
    SetScriptResult(2000);	//≒ SetLife (EV_REQUEST_LIFEと併用)
  }
}

@Initialize {
  objEnemy = ObjEnemy_Create(OBJ_ENEMY_BOSS);	//Obj_Create のボス版
  ObjEnemy_Regist(objEnemy);			//★オブジェクトのアクティブ化

//☆ 注意 ☆  スクリプトパスのディレクトリの区切りは『\』ではなく『/』で行う
  let imgExRumia = GetCurrentScriptDirectory ~ "img/ExRumia.png";
  ObjPrim_SetTexture(objEnemy, imgExRumia);		//ObjEffect_SetTexture ボスもオブジェクトなので、ボスIDに関連付け
  ObjSprite2D_SetSourceRect(objEnemy, 64, 1, 127, 64);	//SetGraphicRect のオブジェクト版
  ObjSprite2D_SetDestCenter(objEnemy);			//★指定したオブジェクトの原点をSetSourceRectで抜き出した画像の中心にセットする

  let cx = GetStgFrameWidth / 2;			//≒ GetCenterX に相当する計算
  ObjMove_SetDestAtFrame(objEnemy, cx, 60, 58);		//SetMovePosition02 相当だが、オブジェクトにも有効

  mainTask;
  endProcess;
}

@MainLoop {
  ObjEnemy_SetIntersectionCircleToShot(objEnemy, ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 32);	//SetCollisionA GetX GetY
  ObjEnemy_SetIntersectionCircleToPlayer(objEnemy, ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 24);	//SetCollisionB GetX GetY
  yield;
}

task mainTask {
  loop(60) {yield;}

  loop {
    let angle = rand(0, 360);
    loop(32) {
      objectBullet1(angle);
      angle += 360/32;
    }
    loop(180) {yield;}

    angle = 70;
    loop(5) {
      objectBullet2(angle);
      angle += 10;
      loop(15) {yield;}
    }
    loop(720) {yield;}
  }
}

task objectBullet1 (let angle) {
	//面倒なのでph3ならではの記述で
  let obj = CreateShotA1(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 4, angle, DS_BALL_BS_BLUE, 0);

  while(!Obj_IsDeleted(obj)) {		//Obj_BeDeleted(obj)
	//if(Obj_GetX(obj) < GetClipMinX) {
    if(ObjMove_GetX(obj) < 0) {
      let speed = 0.4;
      loop(12) {
	//CreateShot01(Obj_GetX(obj), Obj_GetY(obj), speed, 0, BLUE12, 0);
	CreateShotA1(ObjMove_GetX(obj), ObjMove_GetY(obj), speed, 0, DS_RICE_S_BLUE, 0);
	speed += 0.1;
      }
      Obj_Delete(obj);
	//} else if(Obj_GetX(obj) > GetClipMaxX) {
    } else if(ObjMove_GetX(obj) > GetStgFrameWidth) {
      let speed = 0.4;
      loop(12) {
	//CreateShot01(Obj_GetX(obj), Obj_GetY(obj), speed, 180, BLUE12, 0);
	CreateShotA1(ObjMove_GetX(obj), ObjMove_GetY(obj), speed, 180, DS_RICE_S_BLUE, 0);
	speed += 0.1;
      }
      Obj_Delete(obj);
    }
    yield;
  }
}

task objectBullet2 (let angle) {
	//面倒なのでph3ならではの記述で
  let obj = CreateShotA1(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 3, angle, DS_BALL_L_RED, 0);
  ObjShot_SetSpellResist(obj, true);	//ObjShot_SetBombResist

  while(!Obj_IsDeleted(obj)) {		//Obj_BeDeleted(obj)
	//if(Obj_GetY(obj) > GetClipMaxY) {
    if(ObjMove_GetY(obj) > GetStgFrameHeight) {
      let angle = rand(0, 360);
      loop(32) {
	//CreateShot01(Obj_GetX(obj), Obj_GetY(obj), speed, 180, BLUE12, 0);
	CreateShotA1(ObjMove_GetX(obj), ObjMove_GetY(obj), 0.7, angle, DS_BALL_S_RED, 0);
	angle += 360/32;
      }
      Obj_Delete(obj);
    }
    yield;
  }
}

task endProcess {
  while(ObjEnemy_GetInfo(objEnemy, INFO_LIFE) > 0) {yield;}	//≒ GetLife 相当
  Obj_Delete(objEnemy);
  CloseScript(GetOwnScriptID);		//★スクリプトの終了を行う　★自分のスクリプトIDを取得
  return;
}

#include "./function.txt"
