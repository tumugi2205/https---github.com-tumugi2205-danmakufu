#東方弾幕風[Single]	//プログラム中のコメントアウトについて
#ScriptVersion[3]	//★　ver2には無かった処理をする関数
#Title["弾吐き弾 (GATP無し)"]	//≒　ver2に似たような関数がある
#Text[""]		//無印　ver2にほぼ同じ関数がある (ただし処理の適用範囲が拡張されているものが多いため、注釈付き)
#Image[""]		//未記入　verに同名、同じ処理の関数がある
#System["/script/default_system/Default_System.txt"]
#Background["script/default_system/Default_Background_IceMountain.txt"]
#BGM[DEFAULT]
#Player[DEFAULT]

#include "script/default_system/Default_ShotConst.txt"

let objEnemy;

@Event {
  alternative(GetEventType)	//★イベントの種類を取得する関数
  case(EV_REQUEST_LIFE) {	//★引数の文字列はライフ設定要求
    SetScriptResult(2000);	//≒ SetLife (EV_REQUEST_LIFEと併用)
  }
}

@Initialize {
  objEnemy = ObjEnemy_Create(OBJ_ENEMY_BOSS);	//Obj_Create のボス版
  ObjEnemy_Regist(objEnemy);			//★オブジェクトのアクティブ化

//☆ 注意 ☆  スクリプトパスのディレクトリの区切りは『\』ではなく『/』で行う
  let imgExRumia = GetCurrentScriptDirectory ~ "img/ExRumia.png";
  ObjPrim_SetTexture(objEnemy, imgExRumia);		//ObjEffect_SetTexture ボスもオブジェクトなので、ボスIDに関連付け
  ObjSprite2D_SetSourceRect(objEnemy, 64, 1, 127, 64);	//SetGraphicRect のオブジェクト版
  ObjSprite2D_SetDestCenter(objEnemy);			//★指定したオブジェクトの原点をSetSourceRectで抜き出した画像の中心にセットする

  let cx = GetStgFrameWidth / 2;			//≒ GetCenterX に相当する計算
  ObjMove_SetDestAtFrame(objEnemy, cx, 60, 58);		//SetMovePosition02 相当だが、オブジェクトにも有効

  mainTask;
  endProcess;
}

@MainLoop {
  ObjEnemy_SetIntersectionCircleToShot(objEnemy, ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 32);	//SetCollisionA GetX GetY
  ObjEnemy_SetIntersectionCircleToPlayer(objEnemy, ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 24);	//SetCollisionB GetX GetY
  yield;
}

task mainTask {
  loop(60) {yield;}

//≒ CreateShot01, SetShotDataA, AddShot に近い処理
//吐かれる方 (vomited) は、撃たれるタイミングを気にせず普通にCreateShotA1と書いていい
//ObjShot_AddShotA1で指定したアクティブになるタイミングに撃たれるようにしてくれる
//撃たれる位置は吐く弾の位置を原点 (0, 0) として計算するため、
//vomitedの方では第１，２引数を０にすることで弾から弾が出るようになる
//ObjMove_AddPatternA4は吐かれる弾を自機狙い弾にするために使う
//第８引数で角度の基準とするオブジェクトを設定できるため、
//ここに自機オブジェクトのIDを入れて角度を０にすれば自機狙い弾になる
  let vomit = CreateShotA1(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 3, GetAngleToPlayer(objEnemy)-20, DS_BALL_S_RED, 0);
  let vomited = CreateShotA1(0, 0, 2, 0, DS_NEEDLE_RED, 0);
  ObjMove_AddPatternA4(vomited, 0, NO_CHANGE, 0, 0, 0, 2, GetPlayerObjectID, NO_CHANGE);
  ObjShot_AddShotA1(vomit, vomited, 30);

  loop(180) {yield;}

  loop {
    let shotTime = 90;
    loop(30) {
	//省略　上を参照のこと
      vomit = CreateShotA1(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), rand(2, 3), GetAngleToPlayer(objEnemy)+rand(-45, 45), DS_BALL_S_RED, 0);
      vomited = CreateShotA1(0, 0, 1, 0, DS_NEEDLE_RED, 0);
      ObjMove_AddPatternA4(vomited, 0, NO_CHANGE, 0, 0, 0, 1, GetPlayerObjectID, NO_CHANGE);
      ObjShot_AddShotA1(vomit, vomited, shotTime);

      shotTime -= 2;
      loop(2) {yield;}
    }
    loop(120) {yield;}
  }
}

task endProcess {
  while(ObjEnemy_GetInfo(objEnemy, INFO_LIFE) > 0) {yield;}	//≒ GetLife 相当
  Obj_Delete(objEnemy);
  CloseScript(GetOwnScriptID);		//★スクリプトの終了を行う　★自分のスクリプトIDを取得
  return;
}

#include "./function.txt"
