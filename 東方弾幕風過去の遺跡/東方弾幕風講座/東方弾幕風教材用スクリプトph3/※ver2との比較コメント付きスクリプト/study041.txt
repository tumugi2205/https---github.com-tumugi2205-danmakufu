#東方弾幕風[Single]	//プログラム中のコメントアウトについて
#ScriptVersion[3]	//★　ver2には無かった処理をする関数
#Title["保留"]	//≒　ver2に似たような関数がある
#Text["組み込みエフェクトが無いため[r]現在の中身はstudy037と同じ"]		//無印　ver2にほぼ同じ関数がある (ただし処理の適用範囲が拡張されているものが多いため、注釈付き)
#Image[""]		//未記入　verに同名、同じ処理の関数がある
#System["/script/default_system/Default_System.txt"]
#Background["script/default_system/Default_Background_IceMountain.txt"]
#BGM[DEFAULT]
#Player[DEFAULT]

#include "script/default_system/Default_ShotConst.txt"

let objEnemy;

@Event {
  alternative(GetEventType)	//★イベントの種類を取得する関数
  case(EV_REQUEST_LIFE) {	//★引数の文字列はライフ設定要求
    SetScriptResult(2000);	//≒ SetLife (EV_REQUEST_LIFEと併用)
  }
}

@Initialize {
  objEnemy = ObjEnemy_Create(OBJ_ENEMY_BOSS);	//Obj_Create のボス版
  ObjEnemy_Regist(objEnemy);			//★オブジェクトのアクティブ化

//☆ 注意 ☆  スクリプトパスのディレクトリの区切りは『\』ではなく『/』で行う
  let imgExRumia = GetCurrentScriptDirectory ~ "img/ExRumia.png";
  ObjPrim_SetTexture(objEnemy, imgExRumia);		//ObjEffect_SetTexture ボスもオブジェクトなので、ボスIDに関連付け
  ObjSprite2D_SetSourceRect(objEnemy, 64, 1, 127, 64);	//SetGraphicRect のオブジェクト版
  ObjSprite2D_SetDestCenter(objEnemy);			//★指定したオブジェクトの原点をSetSourceRectで抜き出した画像の中心にセットする

  let cx = GetStgFrameWidth / 2;			//≒ GetCenterX に相当する計算
  ObjMove_SetDestAtFrame(objEnemy, cx, 60, 58);		//SetMovePosition02 相当だが、オブジェクトにも有効

  mainTask;
  endProcess;
}

@MainLoop {
  ObjEnemy_SetIntersectionCircleToShot(objEnemy, ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 32);	//SetCollisionA GetX GetY
  ObjEnemy_SetIntersectionCircleToPlayer(objEnemy, ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 24);	//SetCollisionB GetX GetY
  yield;
}

task mainTask {
  loop(60) {yield;}
  let count = 0;
  loop {
    if(count%60 == 0) {
	//GetAngleToPlayer
      let angle = GetAngleToPlayer - 90;
      loop(97) {
	//CreateShot01(GetX, GetY, 1, angle, RED22, 0);
	CreateShotA1(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 1, angle, DS_BUTTERFLY_RED, 0);
	angle += 180/96;
      }
    }
    if(count%150 == 135) {
      objectBullet;
    }
    count++;
    yield;
  }
}

task objectBullet {
	//面倒なのでph3ならではの記述で
  let obj = CreateShotA1(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 3, atan2(GetPlayerY-ObjMove_GetY(objEnemy), GetPlayerX-ObjMove_GetX(objEnemy)), DS_BALL_M_RED, 0);
  ObjShot_SetSpellResist(obj, true);	//ObjShot_SetBombResist

  while(!Obj_IsDeleted(obj)) {		//Obj_BeDeleted(obj)
	//DeleteEnemyShotInCircle(SHOT, Obj_GetX(obj), Obj_GetY(obj), 12);
    DeleteShotInCircle(TYPE_SHOT, TYPE_FADE, ObjMove_GetX(obj), ObjMove_GetY(obj), 12);
    yield;
  }
}

task endProcess {
  while(ObjEnemy_GetInfo(objEnemy, INFO_LIFE) > 0) {yield;}	//≒ GetLife 相当
  Obj_Delete(objEnemy);
	//DeleteEnemyShot(ALL);
  DeleteShotAll(TYPE_ALL, TYPE_FADE);	//弾幕の構成上、弾消しを発生させないと都合が悪い
  CloseScript(GetOwnScriptID);		//★スクリプトの終了を行う　★自分のスクリプトIDを取得
  return;
}

#include "./function.txt"
