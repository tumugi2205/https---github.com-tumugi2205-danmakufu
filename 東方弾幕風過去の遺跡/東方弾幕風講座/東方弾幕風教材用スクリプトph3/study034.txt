#東方弾幕風[Single]
#ScriptVersion[3]
#Title["オブジェクト弾 (回転ビット　その２)"]
#Text[""]
#Image[""]
#System["/script/default_system/Default_System.txt"]
#Background["script/default_system/Default_Background_IceMountain.txt"]
#BGM[DEFAULT]
#Player[DEFAULT]

#include "script/default_system/Default_ShotConst.txt"

let objEnemy;

@Event {
  alternative(GetEventType)
  case(EV_REQUEST_LIFE) {
    SetScriptResult(2000);
  }
}

@Initialize {
  objEnemy = ObjEnemy_Create(OBJ_ENEMY_BOSS);
  ObjEnemy_Regist(objEnemy);

  let imgExRumia = GetCurrentScriptDirectory ~ "img/ExRumia.png";
  ObjPrim_SetTexture(objEnemy, imgExRumia);
  ObjSprite2D_SetSourceRect(objEnemy, 64, 1, 127, 64);
  ObjSprite2D_SetDestCenter(objEnemy);

  let cx = GetStgFrameWidth / 2;
  ObjMove_SetDestAtFrame(objEnemy, cx, 100, 58);

  mainTask;
  endProcess;
}

@MainLoop {
  ObjEnemy_SetIntersectionCircleToShot(objEnemy, ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 32);
  ObjEnemy_SetIntersectionCircleToPlayer(objEnemy, ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 24);
  yield;
}

task mainTask {
  loop(60) {yield;}

  let angle = rand(0, 360);
  loop(6) {
    objectBullet(angle);
    angle += 360/6;
  }
}

task objectBullet (let angle) {
  let obj = CreateShotA1(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 0, angle, DS_BALL_M_RED, 0);
  ObjShot_SetSpellResist(obj, true);

  let count = 0;
  let radiusX = 0;
  let radiusY = 0;
  let angleSpeed = 2.2;
  while((!Obj_IsDeleted(obj)) && (!Obj_IsDeleted(objEnemy))) {
    ObjMove_SetX(obj, ObjMove_GetX(objEnemy)+radiusX*cos(angle));
    ObjMove_SetY(obj, ObjMove_GetY(objEnemy)+radiusY*sin(angle));
    if((count >= 120) && (count%6 == 0)) {
      CreateShotA1(ObjMove_GetX(obj), ObjMove_GetY(obj), 1.5, angle+count/2, DS_BALL_S_RED, 0);
    }

    if(count < 60) {
      radiusX += 100/60;
      radiusY += 60/60;
    } else if((count%600 >= 240) && (count%600 < 300)) {
      radiusX -= 40/60;
      radiusY += 60/60;
    } else if(count%600 >= 540) {
      radiusX += 40/60;
      radiusY -= 60/60;
    }

    if((count%720 >= 240) && (count%720 < 360)) {
      angleSpeed -= 4.4/120;
    } else if(count%720 >= 600) {
      angleSpeed += 4.4/120;
    }

    angle += angleSpeed;
    count++;
    yield;
  }
  Obj_Delete(obj);
}

task endProcess {
  while(ObjEnemy_GetInfo(objEnemy, INFO_LIFE) > 0) {yield;}
  Obj_Delete(objEnemy);
  yield;
  CloseScript(GetOwnScriptID);
  return;
}

#include "./function.txt"
