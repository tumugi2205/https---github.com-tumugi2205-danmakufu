#東方弾幕風
#Title[４４：スペルカード演出 (基礎)]
#Text[]
#PlayLevel[弾幕風教材用]
#Image[.\img\study044.png]
#BackGround[Default]
#ScriptVersion[2]

script_enemy_main {
	let imgExRumia = GetCurrentScriptDirectory ~ "img\ExRumia.png";

	@Initialize {
		SetMovePosition02(GetCenterX-120, GetClipMinY + 100, 120);
		SetLife(3000);

		SetScore(10000);
		SetTimer(60);
		CutIn(KOUMA, "テスト「スペルカード宣言」", 0, 0, 0, 0, 0);

		LoadGraphic(imgExRumia);
		SetTexture(imgExRumia);
		SetGraphicRect(1, 1, 64, 64);

		mainTask;
	}

	@MainLoop {
		SetCollisionA(GetX, GetY, 24);
		SetCollisionB(GetX, GetY, 24);
		yield;
	}

	@DrawLoop {
		SetColor(255,255,255);
		SetRenderState(ALPHA);
		SetTexture(imgExRumia);
		if(GetSpeedX == 0) {
			SetGraphicRect(64, 1, 127, 64);
		} else if(GetSpeedX > 0) {
			SetGraphicRect(192, 1, 255, 64);
		} else if(GetSpeedX < 0) {
			SetGraphicRect(128, 1, 191, 64);
		}
		DrawGraphic(GetX, GetY);
	}

	@Finalize  {
		DeleteGraphic(imgExRumia);
	}

	task mainTask {
		loop(150) {yield;}

		loop {
			SetMovePosition02(GetCenterX+120, GetY, 120);
			loop(6) {
				let angle = rand(0, 360);
				loop(32) {
					let bullet = 1;
					CreateShotA(bullet, GetX, GetY, 0);
					SetShotDataA(bullet, 0, 4, angle, 0, -0.1, 0, WHITE11);
					SetShotDirectionType(PLAYER);
					SetShotDataA(bullet, 60, 0, 0, 0, 0.1, 4, RED11);
					SetShotDirectionType(ABSOLUTE);
					FireShot(bullet);
				angle += 360/32;
				}
				loop(20) {yield;}
			}
			loop(120) {yield;}

			SetMovePosition02(GetCenterX-120, GetY, 120);
			loop(6) {
				let angle = rand(0, 360);
				loop(32) {
				let bullet = 1;
					CreateShotA(bullet, GetX, GetY, 0);
					SetShotDataA(bullet, 0, 4, angle, 0, -0.1, 0, WHITE11);
					SetShotDataA(bullet, 60, 0, GetAngleToPlayer, 0, 0.1, 4, BLUE11);
					FireShot(bullet);
					angle += 360/32;
				}
				loop(20) {yield;}
			}
			loop(60) {yield;}

			SetMovePosition02(GetCenterX, GetCenterY-80, 60);
			loop(60) {yield;}
			Concentration01(60);
			loop(60) {yield;}

			let count = 0;
			let angle = rand(0, 360);
			loop(60) {
				loop(7) {
					CreateShotA("bullet", GetX, GetY, 0);
					SetShotDataA("bullet", 0, 1+0.05*count, angle, 0, -0.05, -3, GREEN11);
					if(count%2 == 0) {
						SetShotDataA("bullet", 60+count*2, -1.14, angle, 0, -0.05, -3, GREEN11);
					}
					FireShot("bullet");
					angle += 360/7;
				}
				angle += 4.3;
				count++;
			}
			loop(240) {yield;}

			SetMovePosition02(GetCenterX-120, GetClipMinY+100, 60);
			loop(90) {yield;}
		}
	}
}
