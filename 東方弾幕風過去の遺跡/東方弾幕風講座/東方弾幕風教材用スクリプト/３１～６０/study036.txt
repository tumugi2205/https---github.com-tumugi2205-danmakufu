#東方弾幕風
#Title[３６：オブジェクト弾 (再帰タスク)]
#Text[]
#PlayLevel[弾幕風教材用]
#Image[.\img\study036.png]
#BackGround[Default]
#ScriptVersion[2]

script_enemy_main {
	let imgExRumia = GetCurrentScriptDirectory ~ "img\ExRumia.png";

	@Initialize {
		SetMovePosition02(GetCenterX, GetClipMinY + 100, 60);
		SetLife(3000);

		LoadGraphic(imgExRumia);
		SetTexture(imgExRumia);
		SetGraphicRect(1, 1, 64, 64);

		mainTask;
	}

	@MainLoop {
		SetCollisionA(GetX, GetY, 24);
		SetCollisionB(GetX, GetY, 24);
		yield;
	}

	@DrawLoop {
		SetColor(255,255,255);
		SetRenderState(ALPHA);
		SetTexture(imgExRumia);
		if(GetSpeedX == 0) {
			SetGraphicRect(64, 1, 127, 64);
		} else if(GetSpeedX > 0) {
			SetGraphicRect(192, 1, 255, 64);
		} else if(GetSpeedX < 0) {
			SetGraphicRect(128, 1, 191, 64);
		}
		DrawGraphic(GetX, GetY);
	}

	@Finalize  {
		DeleteGraphic(imgExRumia);
	}

	task mainTask {
		loop(60) {yield;}

		let sign = rand_int(0, 1)*2 - 1;
		loop {
			objectBullet(sign, GetX, GetY, rand(80, 100)-100*sign, 20);
			sign *= -1;
			loop(30) {yield;}
		}
	}

	task objectBullet (let sign, let px, let py, let angle1, let angle2) {
		let obj = Obj_Create(OBJ_SHOT);
		Obj_SetX(obj, px);
		Obj_SetY(obj, py);
		Obj_SetSpeed(obj, 2);
		Obj_SetAngle(obj, angle1);
		ObjShot_SetGraphic(obj, RED01);
		ObjShot_SetDelay(obj, 0);

		loop(3) {yield;}
		if(!Obj_BeDeleted(obj)) {
			objectBullet(sign, Obj_GetX(obj), Obj_GetY(obj), angle1+angle2*sign, angle2*0.96);
		}
	}
}
