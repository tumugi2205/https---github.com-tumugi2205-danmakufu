#“Œ•û’e–‹•—
#Title[‚R‚VFƒIƒuƒWƒFƒNƒg’e (’e‚ðÁ‚·’e)]
#Text[]
#PlayLevel[’e–‹•—‹³Þ—p]
#Image[.\img\study037.png]
#BackGround[Default]
#ScriptVersion[2]

script_enemy_main {
	let imgExRumia = GetCurrentScriptDirectory ~ "img\ExRumia.png";

	@Initialize {
		SetMovePosition02(GetCenterX, GetClipMinY + 60, 60);
		SetLife(3000);

		LoadGraphic(imgExRumia);
		SetTexture(imgExRumia);
		SetGraphicRect(1, 1, 64, 64);

		mainTask;
	}

	@MainLoop {
		SetCollisionA(GetX, GetY, 24);
		SetCollisionB(GetX, GetY, 24);
		yield;
	}

	@DrawLoop {
		SetColor(255,255,255);
		SetRenderState(ALPHA);
		SetTexture(imgExRumia);
		if(GetSpeedX == 0) {
			SetGraphicRect(64, 1, 127, 64);
		} else if(GetSpeedX > 0) {
			SetGraphicRect(192, 1, 255, 64);
		} else if(GetSpeedX < 0) {
			SetGraphicRect(128, 1, 191, 64);
		}
		DrawGraphic(GetX, GetY);
	}

	@Finalize  {
		DeleteGraphic(imgExRumia);
	}

	task mainTask {
		loop(60) {yield;}
		let count = 0;
		loop {
			if(count%60 == 0) {
				let angle = GetAngleToPlayer - 90;
				loop(97) {
					CreateShot01(GetX, GetY, 1, angle, RED22, 0);
					angle += 180/96;
				}
			}
			if(count%150 == 135) {
				objectBullet;
			}
			count++;
			yield;
		}
	}

	task objectBullet {
		let obj = Obj_Create(OBJ_SHOT);
		Obj_SetX(obj, GetX);
		Obj_SetY(obj, GetY);
		Obj_SetSpeed(obj, 3);
		Obj_SetAngle(obj, GetAngleToPlayer);
		ObjShot_SetGraphic(obj, BLUE02);
		ObjShot_SetDelay(obj, 0);
		ObjShot_SetBombResist(obj, true);

		while(!Obj_BeDeleted(obj)) {
			DeleteEnemyShotInCircle(SHOT, Obj_GetX(obj), Obj_GetY(obj), 12);
			yield;
		}
	}
}
