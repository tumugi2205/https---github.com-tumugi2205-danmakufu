#“Œ•û’e–‹•—
#Title[‚R‚WFƒIƒuƒWƒFƒNƒg’e (Ž©‹@‚ÌˆÚ“®ó‘Ô‚É”½‰ž‚·‚é)]
#Text[]
#PlayLevel[’e–‹•—‹³Þ—p]
#Image[.\img\study038.png]
#BackGround[Default]
#ScriptVersion[2]

script_enemy_main {
	let imgExRumia = GetCurrentScriptDirectory ~ "img\ExRumia.png";

	@Initialize {
		SetMovePosition02(GetCenterX, GetClipMinY + 60, 60);
		SetLife(3000);

		LoadGraphic(imgExRumia);
		SetTexture(imgExRumia);
		SetGraphicRect(1, 1, 64, 64);

		mainTask;
	}

	@MainLoop {
		SetCollisionA(GetX, GetY, 24);
		SetCollisionB(GetX, GetY, 24);
		yield;
	}

	@DrawLoop {
		SetColor(255,255,255);
		SetRenderState(ALPHA);
		SetTexture(imgExRumia);
		if(GetSpeedX == 0) {
			SetGraphicRect(64, 1, 127, 64);
		} else if(GetSpeedX > 0) {
			SetGraphicRect(192, 1, 255, 64);
		} else if(GetSpeedX < 0) {
			SetGraphicRect(128, 1, 191, 64);
		}
		DrawGraphic(GetX, GetY);
	}

	@Finalize  {
		DeleteGraphic(imgExRumia);
	}

	task mainTask {
		loop(60) {yield;}
		loop {
			objectBullet;
			yield;
		}
	}

	task objectBullet {
		let obj = Obj_Create(OBJ_SHOT);
		Obj_SetX(obj, GetX);
		Obj_SetY(obj, GetY);
		Obj_SetSpeed(obj, 4);
		Obj_SetAngle(obj, GetAngleToPlayer+rand(-90, 90));
		ObjShot_SetGraphic(obj, RED04);
		ObjShot_SetDelay(obj, 0);

		while(!Obj_BeDeleted(obj)) {
			if(GetPlayerMoveState == MOVE_NORMAL) {
				Obj_SetSpeed(obj, 1.5);
				ObjShot_SetGraphic(obj, BLUE04);
			} else {
				Obj_SetSpeed(obj, 4);
				ObjShot_SetGraphic(obj, RED04);
			}
			yield;
		}
	}
}
