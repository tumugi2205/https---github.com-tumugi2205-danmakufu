#東方弾幕風
#Title[４７：ホーミングするオブジェクト弾]
#Text[]
#PlayLevel[弾幕風教材用]
#Image[.\img\study047.png]
#BackGround[Default]
#ScriptVersion[2]

script_enemy_main {
	let imgExRumia = GetCurrentScriptDirectory ~ "img\ExRumia.png";

	@Initialize {
		SetMovePosition02(GetCenterX, GetClipMinY + 60, 60);
		SetLife(3000);

		LoadGraphic(imgExRumia);
		SetTexture(imgExRumia);
		SetGraphicRect(1, 1, 64, 64);

		mainTask;
	}

	@MainLoop {
		SetCollisionA(GetX, GetY, 24);
		SetCollisionB(GetX, GetY, 24);
		yield;
	}

	@DrawLoop {
		SetColor(255,255,255);
		SetRenderState(ALPHA);
		SetTexture(imgExRumia);
		if(GetSpeedX == 0) {
			SetGraphicRect(64, 1, 127, 64);
		} else if(GetSpeedX > 0) {
			SetGraphicRect(192, 1, 255, 64);
		} else if(GetSpeedX < 0) {
			SetGraphicRect(128, 1, 191, 64);
		}
		DrawGraphic(GetX, GetY);
	}

	@Finalize  {
		DeleteGraphic(imgExRumia);
	}

	task mainTask {
		SetShotAutoDeleteClip(12, 12, 12, 12);
		loop(60) {yield;}

		objectBullet1;
		loop(240) {yield;}
		loop {
			objectBullet2(rand(0, 360));
			loop(15) {yield;}
		}
	}

	task objectBullet1 {
		let obj = Obj_Create(OBJ_SHOT);
		Obj_SetX(obj, GetX);
		Obj_SetY(obj, GetY);
		Obj_SetSpeed(obj, 1);
		Obj_SetAngle(obj, 90);
		ObjShot_SetGraphic(obj, RED02);
		ObjShot_SetDelay(obj, 0);
		ObjShot_SetBombResist(obj, true);

		while(!Obj_BeDeleted(obj)) {
			Obj_SetAngle(obj, atan2(GetPlayerY-Obj_GetY(obj), GetPlayerX-Obj_GetX(obj)));
			yield;
		}
	}

	task objectBullet2 (let angle) {
		let obj = Obj_Create(OBJ_SHOT);
		Obj_SetX(obj, GetX);
		Obj_SetY(obj, GetY);
		Obj_SetSpeed(obj, 3);
		Obj_SetAngle(obj, angle);
		ObjShot_SetGraphic(obj, BLUE04);
		ObjShot_SetDelay(obj, 0);

		let difference = 4;
		while(!Obj_BeDeleted(obj)) {
			let aim = (atan2(GetPlayerY-Obj_GetY(obj), GetPlayerX-Obj_GetX(obj))+360) % 360;
			let ret = aim - angle;
			if(((ret > 0) && (ret <= 180)) || ((ret < 0) && (ret <= -180))) {
				angle += difference;
			} else if(((ret > 0) && (ret > 180)) || ((ret < 0) && (ret > -180))) {
				angle -= difference;
			}
			angle = (angle+360) % 360;

			Obj_SetAngle(obj, angle);
			difference *= 0.99;
			yield;
		}
	}
}
