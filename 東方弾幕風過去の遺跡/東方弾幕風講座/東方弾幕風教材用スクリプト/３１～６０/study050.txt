#“Œ•û’e–‹•—
#Title[‚T‚OFƒxƒNƒgƒ‹ŒvŽZ‚É‚æ‚é’e (‚»‚Ì‚Q)]
#Text[]
#PlayLevel[’e–‹•—‹³Þ—p]
#Image[.\img\study050.png]
#BackGround[Default]
#ScriptVersion[2]

script_enemy_main {
	let imgExRumia = GetCurrentScriptDirectory ~ "img\ExRumia.png";

	@Initialize {
		SetMovePosition02(GetCenterX, GetClipMinY + 150, 120);
		SetLife(3000);

		LoadGraphic(imgExRumia);
		SetTexture(imgExRumia);
		SetGraphicRect(1, 1, 64, 64);

		mainTask;
	}

	@MainLoop {
		SetCollisionA(GetX, GetY, 24);
		SetCollisionB(GetX, GetY, 24);
		yield;
	}

	@DrawLoop {
		SetColor(255,255,255);
		SetRenderState(ALPHA);
		SetTexture(imgExRumia);
		if(GetSpeedX == 0) {
			SetGraphicRect(64, 1, 127, 64);
		} else if(GetSpeedX > 0) {
			SetGraphicRect(192, 1, 255, 64);
		} else if(GetSpeedX < 0) {
			SetGraphicRect(128, 1, 191, 64);
		}
		DrawGraphic(GetX, GetY);
	}

	@Finalize  {
		DeleteGraphic(imgExRumia);
	}

	task mainTask {
		loop(120) {yield;}
		loop {
			let speed1 = 1;
			let angle1 = GetAngleToPlayer;
			let vectorX1 = speed1 * cos(angle1);
			let vectorY1 = speed1 * sin(angle1);
			CreateShot11(GetX, GetY, vectorX1, vectorY1, RED01, 0);
			loop(120) {yield;}

			let speed2 = -3;
			let angle2 = GetAngleToPlayer + 90;
			let vectorX2 = 0;
			let vectorY2 = 0;
			while(speed2 <= 3) {
				vectorX2 = speed2 * cos(angle2);
				vectorY2 = speed2 * sin(angle2);
				CreateShot11(GetX, GetY, vectorX2, vectorY2, RED01, 0);
				speed2 += 6/31;
			}
			loop(120) {yield;}

			speed1 = 1;
			angle1 = GetAngleToPlayer;
			vectorX1 = speed1 * cos(angle1);
			vectorY1 = speed1 * sin(angle1);
			speed2 = -3;
			angle2 = GetAngleToPlayer + 90;
			vectorX2 = 0;
			vectorY2 = 0;
			while(speed2 <= 3) {
				vectorX2 = speed2 * cos(angle2);
				vectorY2 = speed2 * sin(angle2);
				CreateShot11(GetX, GetY, vectorX1+vectorX2, vectorY1+vectorY2, RED01, 0);
				speed2 += 6/31;
			}
			loop(240) {yield;}

			speed1 = 1;
			angle1 = -90;
			vectorX1 = 0;
			vectorY1 = 0;
			loop(5) {
				vectorX1 = speed1 * cos(angle1);
				vectorY1 = speed1 * sin(angle1);
				speed2 = -1*cos(18)/sin(18) * speed1;
				angle2 = angle1 + 90;
				vectorX2 = 0;
				vectorY2 = 0;
				while(speed2 < cos(18)/sin(18)*speed1) {
					vectorX2 = speed2 * cos(angle2);
					vectorY2 = speed2 * sin(angle2);
					CreateShot11(GetX, GetY, vectorX1+vectorX2, vectorY1+vectorY2, RED01, 0);
					speed2 += cos(18)/sin(18)*speed1/31;
				}
				angle1 += 360/5*2;
			}
			loop(360) {yield;}
		}
	}
}
