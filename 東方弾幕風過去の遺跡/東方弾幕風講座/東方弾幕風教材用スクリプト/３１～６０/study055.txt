#東方弾幕風
#Title[５５：オブジェクトレーザー (その１)]
#Text[]
#PlayLevel[弾幕風教材用]
#Image[.\img\study055.png]
#BackGround[Default]
#ScriptVersion[2]

script_enemy_main {
	let imgExRumia = GetCurrentScriptDirectory ~ "img\ExRumia.png";

	@Initialize {
		SetMovePosition02(GetCenterX, GetClipMinY + 100, 60);
		SetLife(3000);

		LoadGraphic(imgExRumia);
		SetTexture(imgExRumia);
		SetGraphicRect(1, 1, 64, 64);

		mainTask;
	}

	@MainLoop {
		SetCollisionA(GetX, GetY, 24);
		SetCollisionB(GetX, GetY, 24);
		yield;
	}

	@DrawLoop {
		SetColor(255,255,255);
		SetRenderState(ALPHA);
		SetTexture(imgExRumia);
		if(GetSpeedX == 0) {
			SetGraphicRect(64, 1, 127, 64);
		} else if(GetSpeedX > 0) {
			SetGraphicRect(192, 1, 255, 64);
		} else if(GetSpeedX < 0) {
			SetGraphicRect(128, 1, 191, 64);
		}
		DrawGraphic(GetX, GetY);
	}

	@Finalize  {
		DeleteGraphic(imgExRumia);
	}

	task mainTask {
		loop(60) {yield;}

		loop {
			let px = rand(GetClipMinX+50, GetClipMaxX-50);
			let py = rand(GetClipMinY+30, GetClipMaxY-30);
			SetMovePosition02(px, py, 60);
			loop(60) {yield;}

			objectLaser;
		}
	}

	task objectLaser {
		let obj = Obj_Create(OBJ_LASER);
		Obj_SetX(obj, GetX);
		Obj_SetY(obj, GetY);
		Obj_SetSpeed(obj, 4);
		Obj_SetAngle(obj, GetAngleToPlayer);
		ObjLaser_SetLength(obj, 600);
		ObjLaser_SetWidth(obj, 25);
		ObjShot_SetGraphic(obj, BLUE05);
		ObjShot_SetDelay(obj, 30);

		loop(360) {yield;}
		ObjShot_FadeDelete(obj);
	}
}
