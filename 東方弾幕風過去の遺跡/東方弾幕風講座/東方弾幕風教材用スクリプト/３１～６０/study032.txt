#“Œ•û’e–‹•—
#Title[‚R‚QFƒIƒuƒWƒFƒNƒg’e (“–‚½‚è”»’è‚ÌÁŽ¸)]
#Text[]
#PlayLevel[’e–‹•—‹³Þ—p]
#Image[.\img\study032.png]
#BackGround[User]
#ScriptVersion[2]

script_enemy_main {
	let imgExRumia = GetCurrentScriptDirectory ~ "img\ExRumia.png";

	@Initialize {
		SetMovePosition02(GetCenterX, GetClipMinY + 60, 60);
		SetLife(3000);

		LoadGraphic(imgExRumia);
		SetTexture(imgExRumia);
		SetGraphicRect(1, 1, 64, 64);

		mainTask;
	}

	@MainLoop {
		SetCollisionA(GetX, GetY, 24);
		SetCollisionB(GetX, GetY, 24);
		yield;
	}

	@DrawLoop {
		SetColor(255,255,255);
		SetRenderState(ALPHA);
		SetTexture(imgExRumia);
		if(GetSpeedX == 0) {
			SetGraphicRect(64, 1, 127, 64);
		} else if(GetSpeedX > 0) {
			SetGraphicRect(192, 1, 255, 64);
		} else if(GetSpeedX < 0) {
			SetGraphicRect(128, 1, 191, 64);
		}
		DrawGraphic(GetX, GetY);
	}

	@Finalize  {
		DeleteGraphic(imgExRumia);
	}

	task mainTask {
		loop(60) {yield;}

		SetMovePosition02(GetCenterX, GetCenterY - 40, 60);
		loop(60) {yield;}
		loop {
			loop(4) {
				let angle = rand(0, 360);
				let cycle = rand_int(0, 119);
				loop(120) {
					objectBullet(angle, cycle);
					angle += 360/120;
				}
				loop(120) {yield;}
			}
			loop(180) {yield;}

			let count = 0;
			let sign = rand_int(0, 1)*2 - 1;
			loop(16) {
				let type = 1 + truncate(count/4);
				let angle = rand(0, 360);
				let cycle = rand_int(0, 119);

				loop(120) {
					objectBullet(angle, cycle);
					angle += 360/120;
					cycle = (cycle + type*sign + 120) % 120;
				}

				sign *= -1;
				count++;
				loop(120) {yield;}
			}
			loop(180) {yield;}
		}
	}

	task objectBullet (let angle, let cycle) {
		let obj = Obj_Create(OBJ_SHOT);
		Obj_SetX(obj, GetX);
		Obj_SetY(obj, GetY);
		Obj_SetSpeed(obj, 1);
		Obj_SetAngle(obj, angle);
		ObjShot_SetGraphic(obj, RED02);
		ObjShot_SetDelay(obj, 0);

		while(!Obj_BeDeleted(obj)) {
			let alpha = 15 + 240*cycle/60;
			if(cycle >= 60) {
				alpha = 255 - 240*(cycle-60)/60;
			}

			Obj_SetAlpha(obj, alpha);
			if(alpha >= 192) {
				Obj_SetCollisionToPlayer(obj, true);
				ObjShot_SetGraphic(obj, RED02);
			} else {
				Obj_SetCollisionToPlayer(obj, false);
				ObjShot_SetGraphic(obj, ORANGE02);
			}

			cycle = (cycle+1) % 120;
			yield;
		}
	}
}
