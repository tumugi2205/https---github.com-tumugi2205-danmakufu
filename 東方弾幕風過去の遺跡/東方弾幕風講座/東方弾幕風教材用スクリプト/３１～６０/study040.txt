#“Œ•û’e–‹•—
#Title[‚S‚OF”½Œ‚‚·‚é’e–‹]
#Text[]
#PlayLevel[’e–‹•—‹³Þ—p]
#Image[.\img\study040.png]
#BackGround[Default]
#ScriptVersion[2]

script_enemy_main {
	let imgExRumia = GetCurrentScriptDirectory ~ "img\ExRumia.png";

	@Initialize {
		SetMovePosition02(GetCenterX, GetClipMinY + 60, 60);
		SetLife(3000);

		LoadGraphic(imgExRumia);
		SetTexture(imgExRumia);
		SetGraphicRect(1, 1, 64, 64);

		mainTask;
	}

	@MainLoop {
		SetCollisionA(GetX, GetY, 24);
		SetCollisionB(GetX, GetY, 24);
		yield;
	}

	@DrawLoop {
		SetColor(255,255,255);
		SetRenderState(ALPHA);
		SetTexture(imgExRumia);
		if(GetSpeedX == 0) {
			SetGraphicRect(64, 1, 127, 64);
		} else if(GetSpeedX > 0) {
			SetGraphicRect(192, 1, 255, 64);
		} else if(GetSpeedX < 0) {
			SetGraphicRect(128, 1, 191, 64);
		}
		DrawGraphic(GetX, GetY);
	}

	@Finalize  {
		DeleteGraphic(imgExRumia);
	}

	task mainTask {
		loop(60) {yield;}

		let count = 0;
		let counter = 0;
		loop {
			if(count%20 == 0) {
				let angle = GetAngleToPlayer - 45 + rand(-15, 15);
				loop(7) {
					CreateShot01(GetX, GetY, 2, angle, RED12, 0);
					angle += 15;
				}
			}

			if(counter >= 30) {
				let angle = rand(0, 360);
				loop(16) {
					CreateShot01(GetX, GetY, 4, angle, WHITE02, 0);
					angle += 360/16;
				}
				counter -= 30;
			}

			count++;
			counter += GetHitCount;
			yield;
		}
	}
}
