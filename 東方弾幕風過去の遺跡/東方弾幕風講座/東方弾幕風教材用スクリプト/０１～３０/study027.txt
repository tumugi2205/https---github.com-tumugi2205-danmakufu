#“Œ•û’e–‹•—
#Title[‚Q‚VFƒIƒuƒWƒFƒNƒg’e (AddShot‚Å‚Í‚È‚¢’e“f‚«’e)]
#Text[]
#PlayLevel[’e–‹•—‹³Þ—p]
#Image[.\img\study027.png]
#BackGround[Default]
#ScriptVersion[2]

script_enemy_main {
	let imgExRumia = GetCurrentScriptDirectory ~ "img\ExRumia.png";

	@Initialize {
		SetMovePosition02(GetCenterX, GetClipMinY + 60, 60);
		SetLife(3000);

		LoadGraphic(imgExRumia);
		SetTexture(imgExRumia);
		SetGraphicRect(1, 1, 64, 64);

		mainTask;
	}

	@MainLoop {
		SetCollisionA(GetX, GetY, 24);
		SetCollisionB(GetX, GetY, 24);
		yield;
	}

	@DrawLoop {
		SetColor(255,255,255);
		SetRenderState(ALPHA);
		SetTexture(imgExRumia);
		if(GetSpeedX == 0) {
			SetGraphicRect(64, 1, 127, 64);
		} else if(GetSpeedX > 0) {
			SetGraphicRect(192, 1, 255, 64);
		} else if(GetSpeedX < 0) {
			SetGraphicRect(128, 1, 191, 64);
		}
		DrawGraphic(GetX, GetY);
	}

	@Finalize  {
		DeleteGraphic(imgExRumia);
	}

	task mainTask {
		loop(60) {yield;}

		let angle = rand_int(0, 1)*180;
		loop {
			objectBullet(angle);
			angle = (angle+180) % 360;
			loop(60) {yield;}
		}
	}

	task objectBullet (let angle) {
		let obj = Obj_Create(OBJ_SHOT);
		Obj_SetX(obj, GetX);
		Obj_SetY(obj, GetY);
		Obj_SetSpeed(obj, 6);
		Obj_SetAngle(obj, angle);
		ObjShot_SetGraphic(obj, RED02);
		ObjShot_SetDelay(obj, 0);

		let speed = 3;
		while(!Obj_BeDeleted(obj)) {
			CreateShot01(Obj_GetX(obj), Obj_GetY(obj), speed, 90, RED11, 0);
			speed += 0.2;
			yield;
		}
	}
}
