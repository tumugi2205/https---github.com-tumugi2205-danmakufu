#“Œ•û’e–‹•—
#Title[‚Q‚XFƒIƒuƒWƒFƒNƒg’e (‰æ–Ê˜g’…’eŽ®ày—ô’e)]
#Text[]
#PlayLevel[’e–‹•—‹³Þ—p]
#Image[.\img\study029.png]
#BackGround[Default]
#ScriptVersion[2]

script_enemy_main {
	let imgExRumia = GetCurrentScriptDirectory ~ "img\ExRumia.png";

	@Initialize {
		SetMovePosition02(GetCenterX, GetClipMinY + 60, 60);
		SetLife(3000);

		LoadGraphic(imgExRumia);
		SetTexture(imgExRumia);
		SetGraphicRect(1, 1, 64, 64);

		mainTask;
	}

	@MainLoop {
		SetCollisionA(GetX, GetY, 24);
		SetCollisionB(GetX, GetY, 24);
		yield;
	}

	@DrawLoop {
		SetColor(255,255,255);
		SetRenderState(ALPHA);
		SetTexture(imgExRumia);
		if(GetSpeedX == 0) {
			SetGraphicRect(64, 1, 127, 64);
		} else if(GetSpeedX > 0) {
			SetGraphicRect(192, 1, 255, 64);
		} else if(GetSpeedX < 0) {
			SetGraphicRect(128, 1, 191, 64);
		}
		DrawGraphic(GetX, GetY);
	}

	@Finalize  {
		DeleteGraphic(imgExRumia);
	}

	task mainTask {
		loop(60) {yield;}

		loop {
			let angle = rand(0, 360);
			loop(32) {
				objectBullet1(angle);
				angle += 360/32;
			}
			loop(180) {yield;}

			angle = 70;
			loop(5) {
				objectBullet2(angle);
				angle += 10;
				loop(15) {yield;}
			}
			loop(720) {yield;}
		}
	}

	task objectBullet1 (let angle) {
		let obj = Obj_Create(OBJ_SHOT);
		Obj_SetX(obj, GetX);
		Obj_SetY(obj, GetY);
		Obj_SetSpeed(obj, 4);
		Obj_SetAngle(obj, angle);
		ObjShot_SetGraphic(obj, BLUE04);
		ObjShot_SetDelay(obj, 0);

		while(!Obj_BeDeleted(obj)) {
			if(Obj_GetX(obj) < GetClipMinX) {
				let speed = 0.4;
				loop(12) {
					CreateShot01(Obj_GetX(obj), Obj_GetY(obj), speed, 0, BLUE12, 0);
					speed += 0.1;
				}
				Obj_Delete(obj);
			} else if(Obj_GetX(obj) > GetClipMaxX) {
				let speed = 0.4;
				loop(12) {
					CreateShot01(Obj_GetX(obj), Obj_GetY(obj), speed, 180, BLUE12, 0);
					speed += 0.1;
				}
				Obj_Delete(obj);
			}
			yield;
		}
	}

	task objectBullet2 (let angle) {
		let obj = Obj_Create(OBJ_SHOT);
		Obj_SetX(obj, GetX);
		Obj_SetY(obj, GetY);
		Obj_SetSpeed(obj, 3);
		Obj_SetAngle(obj, angle);
		ObjShot_SetGraphic(obj, RED03);
		ObjShot_SetDelay(obj, 0);
		ObjShot_SetBombResist(obj, true);

		while(!Obj_BeDeleted(obj)) {
			if(Obj_GetY(obj) > GetClipMaxY) {
				let angle = rand(0, 360);
				loop(32) {
					CreateShot01(Obj_GetX(obj), Obj_GetY(obj), 0.7, angle, RED01, 0);
					angle += 360/32;
				}
				Obj_Delete(obj);
			}
			yield;
		}
	}
}
