#“Œ•û’e–‹•—
#Title[‚Q‚SFƒ†[ƒU[’è‹`ŠÖ”]
#Text[]
#PlayLevel[’e–‹•—‹³Þ—p]
#Image[.\img\study024.png]
#BackGround[Default]
#ScriptVersion[2]

script_enemy_main {
	let imgExRumia = GetCurrentScriptDirectory ~ "img\ExRumia.png";

	@Initialize {
		SetMovePosition02(GetCenterX, GetClipMinY + 60, 60);
		SetLife(3000);

		LoadGraphic(imgExRumia);
		SetTexture(imgExRumia);
		SetGraphicRect(1, 1, 64, 64);

		mainTask;
	}

	@MainLoop {
		SetCollisionA(GetX, GetY, 24);
		SetCollisionB(GetX, GetY, 24);
		yield;
	}

	@DrawLoop {
		SetColor(255,255,255);
		SetRenderState(ALPHA);
		SetTexture(imgExRumia);
		if(GetSpeedX == 0) {
			SetGraphicRect(64, 1, 127, 64);
		} else if(GetSpeedX > 0) {
			SetGraphicRect(192, 1, 255, 64);
		} else if(GetSpeedX < 0) {
			SetGraphicRect(128, 1, 191, 64);
		}
		DrawGraphic(GetX, GetY);
	}

	@Finalize  {
		DeleteGraphic(imgExRumia);
	}

	task mainTask {
		loop(60) {yield;}

		loop {
			let way = 3;
			let angleSpan = 20;
			while(way <= 15) {
				shotNway(way, angleSpan);
				way++;
				angleSpan--;
				loop(60) {yield;}
			}
			loop(120) {yield;}
		}
	}

	function shotNway (let way, let angleSpan) {
		let angle = GetAngleToPlayer - angleSpan * (way-1) / 2;
		loop(way) {
			CreateShot01(GetX, GetY, 2, angle, RED01, 0);
			angle += angleSpan;
		}
	}
}
