#“Œ•û’e–‹•—
#Title[‚R‚OFƒIƒuƒWƒFƒNƒg’e (‰æ–Ê˜g‚Å”½ŽË‚·‚é’e)]
#Text[]
#PlayLevel[’e–‹•—‹³Þ—p]
#Image[.\img\study030.png]
#BackGround[Default]
#ScriptVersion[2]

script_enemy_main {
	let imgExRumia = GetCurrentScriptDirectory ~ "img\ExRumia.png";

	@Initialize {
		SetMovePosition02(GetCenterX, GetClipMinY + 60, 60);
		SetLife(3000);

		LoadGraphic(imgExRumia);
		SetTexture(imgExRumia);
		SetGraphicRect(1, 1, 64, 64);

		mainTask;
	}

	@MainLoop {
		SetCollisionA(GetX, GetY, 24);
		SetCollisionB(GetX, GetY, 24);
		yield;
	}

	@DrawLoop {
		SetColor(255,255,255);
		SetRenderState(ALPHA);
		SetTexture(imgExRumia);
		if(GetSpeedX == 0) {
			SetGraphicRect(64, 1, 127, 64);
		} else if(GetSpeedX > 0) {
			SetGraphicRect(192, 1, 255, 64);
		} else if(GetSpeedX < 0) {
			SetGraphicRect(128, 1, 191, 64);
		}
		DrawGraphic(GetX, GetY);
	}

	@Finalize  {
		DeleteGraphic(imgExRumia);
	}

	task mainTask {
		loop(60) {yield;}

		loop {
			loop(4) {
				let angle = rand(0, 360);
				loop(24) {
					objectBullet(angle);
					angle += 360/24;
				}
				loop(90) {yield;}
			}
			loop(180) {yield;}

			SetMovePosition02(GetCenterX, GetCenterY - 40, 60);
			loop(60) {yield;}

			let sign = rand_int(0, 1)*2 - 1;
			loop(4) {
				let angle = rand(0, 360);
				loop(6) {
					loop(16) {
						objectBullet(angle);
						angle += 360/16;
					}
					angle += 0.7*sign;
					loop(5) {yield;}
				}
				sign *= -1;
				loop(60) {yield;}
			}
			loop(180) {yield;}

			SetMovePosition02(GetCenterX, GetClipMinY + 60, 60);
			loop(60) {yield;}
		}
	}

	task objectBullet (let angle) {
		let obj = Obj_Create(OBJ_SHOT);
		Obj_SetX(obj, GetX);
		Obj_SetY(obj, GetY);
		Obj_SetSpeed(obj, 2);
		Obj_SetAngle(obj, angle);
		ObjShot_SetGraphic(obj, BLUE11);
		ObjShot_SetDelay(obj, 0);

		while(!Obj_BeDeleted(obj)) {
			if((Obj_GetX(obj) < GetClipMinX) || (Obj_GetX(obj) > GetClipMaxX)) {
				CreateShot01(Obj_GetX(obj), Obj_GetY(obj), 2, 180-Obj_GetAngle(obj), AQUA11, 0);
				Obj_Delete(obj);
			} else if(Obj_GetY(obj) < GetClipMinY) {
				CreateShot01(Obj_GetX(obj), Obj_GetY(obj), 2, 360-Obj_GetAngle(obj), AQUA11, 0);
				Obj_Delete(obj);
			}
			yield;
		}
	}
}
